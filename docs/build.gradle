buildscript {
  ext {
    asciidocBuildDir = "$buildDir/asciidoc"
  }
}

plugins {
  id "org.asciidoctor.convert" version "1.5.6"
  id "org.ajoberstar.git-publish" version "0.2.2"
}

task wrapper(type: Wrapper) {
  gradleVersion = "4.8.1"
  distributionType = "BIN"
}


version "0.0.1"
group "daggerok"

asciidoctorj {
  version = "1.5.5"
}

asciidoctor {
  sourceDir = project.file("src")
  sources {
    include "index.adoc"
  }
  outputDir = project.file("$asciidocBuildDir")
  attributes toc: "left",
      idprefix: "",
      docinfo1: "",
      icons: "font",
      idseparator: "-",
      setanchors: "true",
      "source-highlighter": "coderay",
      "toc-title": "Table of Contents",
      "build-gradle": file("build.gradle"),
      "endpoint-url": "https://daggerok.github.io/axon-examples"
}

asciidoctor.doLast {
  file("$asciidocBuildDir/html5/404.html").text = file("$asciidocBuildDir/html5/index.html").text
  file("$asciidocBuildDir/html5/.nojekyll").text = "\n"
}

task documentation(type: Copy, dependsOn: asciidoctor) {
  shouldRunAfter clean, asciidoctor
  from "$asciidoctor.outputDir/html5"
  from "$asciidoctor.outputDir/pdf"
  from "$asciidoctor.sourceDir/static", {
    into '.'
  }
  into "$rootDir/public"
}

gitPublish {

//  repoUri = "git@github.com:daggerok/axon-examples.git"
  repoUri = "https://github.com/daggerok/axon-examples.git"
  branch = "gh-pages"

  contents {
    from("$rootDir/public") {
      into "."
    }
  }

  commitMessage = "Documentation for ${project.group}:${project.name}:$project.version"
}

gitPublishPush.dependsOn asciidoctor
gitPublishPush.shouldRunAfter clean, asciidoctor, build

task docs(dependsOn: gitPublishPush)
